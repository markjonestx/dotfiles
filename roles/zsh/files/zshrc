# The login nodes use their own special prompt
if [[ $HOSTNAME == sci* ]]; then
    setopt PROMPT_SUBST
    PROMPT='%F{green}%*%f %F{magenta}${(%):-%m}%f %F{blue}%~%f $ '
else
    ##
    # Prompts for normal systems
    ##

    # I use alacritty, but most systems will not recognize it
    if [[ $TERM == alacritty ]]; then
        if ! infocmp alacritty >/dev/null 2>&1; then
            export TERM='xterm-256color'
        fi
    fi

    # Because TMUX can break things too
    if [[ $TERM == tmux-256color ]]; then
        if ! infocmp tmux-256color >/dev/null 2>&1; then
            export TERM='xterm-256color'
        fi
    fi

    # Oh My ZSH stuffs
    export ZSH="$HOME/.local/share/shell/ohmyzsh"
    export ZSH_CUSTOM="$HOME/.local/share/shell/zsh_custom"
   
    # Saving my home folder from all the ZCompDumps
    if [[ ! -d "${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump/" ]]; then
        mkdir "${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump/"
    fi
    ZSH_COMPDUMP="${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump/${HOSTNAME}-${ZSH_VERSION}"
    
    # Powerlevel10k requires a ZSH >= 5.01
    if [[ "$(echo $ZSH_VERSION | awk -F. '{ printf("%d%03d", $1,$2)}')" -ge 5001 ]]; then
    
        # Required for instantaneous prompt
        if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
            source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
        fi
        
        # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
        ZSH_THEME="powerlevel10k/powerlevel10k"
        if [[ -f ~/.local/share/shell/p10k_configuration ]]; then
            source ~/.local/share/shell/p10k_configuration
        fi
    else
        # A simple fallback for when Powerlevel10k can't work
        ZSH_THEME="alanpeabody"
    fi

     # update automatically without asking
     zstyle ':omz:update' mode auto 
    
    plugins=(zsh-syntax-highlighting)
    
    source $ZSH/oh-my-zsh.sh
    setopt no_share_history
    unsetopt share_history
fi

##
# This is where path setup and other details go
##

if [[ ! $HOSTNAME = kube* ]]; then
    if [ -d "/apps/sudo/bin" ]; then
        export PATH=/apps/sudo/bin:$PATH
    fi
fi

if [ -d "$HOME/.local/bin" ]; then
    export PATH=$HOME/.local/bin:$PATH
fi

if command -v nvim &> /dev/null; then
    export EDITOR=$(which nvim)
    alias vim=nvim
else
    export EDITOR=$(which vim)
fi

# Because git has it's own variable...
export GIT_EDITOR=$EDITOR

if [ -d "$HOME/.cargo" ]; then
    . "$HOME/.cargo/env"
fi

if [ -d "$HOME/.local/go/bin" ]; then
    export PATH=$HOME/.local/go/bin:$PATH
fi

