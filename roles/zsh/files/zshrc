# The login nodes use their own special prompt
if [[ $HOSTNAME == sci* ]]; then
    setopt PROMPT_SUBST
    PROMPT='%F{green}%*%f %F{magenta}${(%):-%m}%f %F{blue}%~%f $ '
else
    ##
    # Prompts for normal systems
    ##
   
    # OhMyZSH stuffs
    export ZSH="$HOME/.local/share/shell/ohmyzsh"
    export ZSH_CUSTOM="$HOME/.local/share/shell/zsh_custom"

    # I use alacritty, but most systems will not recognize it
    if [[ $TERM == alacritty ]]; then
        if ! infocmp alacritty >/dev/null 2>&1; then
            export TERM='xterm-256color'
        fi
    fi

    # Because TMUX can break things too
    if [[ $TERM == tmux-256color ]]; then
        if ! infocmp tmux-256color >/dev/null 2>&1; then
            export TERM='xterm-256color'
        fi
    fi
    
    # Powerlevel10k requires a ZSH >= 5.01
    if [[ "$(echo $ZSH_VERSION | awk -F. '{ printf("%d%03d", $1,$2)}')" -ge 5001 ]]; then
    
        # Required for instantaneous prompt
        if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
            source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
        fi
        
        # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
        ZSH_THEME="powerlevel10k/powerlevel10k"
        if [[ -f ~/.local/share/shell/p10k_configuration ]]; then
            source ~/.local/share/shell/p10k_configuration
        fi
    else
        # A simple fallback for when Powerlevel10k can't work
        ZSH_THEME="alanpeabody"
    fi

     # update automatically without asking
     zstyle ':omz:update' mode auto 
    
    plugins=(zsh-syntax-highlighting)
    
    source $ZSH/oh-my-zsh.sh
    setopt no_share_history
    unsetopt share_history
fi

# Set Editor based on what's available
if command -v nvim &> /dev/null; then
    export EDITOR=$(which nvim)
    export MANPAGER="${EDITOR} +Man!"
    alias vim=nvim
else
    export EDITOR=$(which vim)
fi

# Because git has it's own variable...
export GIT_EDITOR=$EDITOR

if [ -d "$HOME/.fzf" ]; then
    export PATH="$PATH:$HOME/.fzf/bin"
fi

# Setup FZF for use here
if [[ -n $TMUX ]]; then
    export PFZF=$(which fzf-tmux)
else
    export PFZF=$(which fzf)
fi

# Functions to make my life _slightly_ easier
fcd() {
    local dir
    if [[ -n $1 ]]; then
        dir=$(fd . $1 -t d --full-path | $PFZF --prompt "Select directory: ")
    else
        dir=$(fd -t d | $PFZF --prompt "Select directory: ")
    fi

    if [[ -n $dir ]]; then
        cd "$dir"
    else
        echo "Directory not found!"
    fi
} 

fcdp() {
    fcd ~/Desktop
}

edit() {
    if [[ -f $1 ]]; then
        $EDITOR $1
    else
        dir=$(fd -t f $1 | $PFZF --prompt "Select file: ")
        if [[ -n $dir ]]; then
            $EDITOR $dir
        else
            echo "File not found!"
        fi
    fi
}

